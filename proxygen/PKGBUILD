# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=proxygen
pkgver=0.18.0
pkgrel=1
pkgdesc="A collection of C++ HTTP libraries including an easy to use HTTP server"
arch=('i686' 'x86_64')
url="https://github.com/facebook/proxygen"
license=('Apache')
depends=('fbthrift' 'libcap')
source=("https://github.com/facebook/proxygen/archive/v$pkgver.tar.gz"
'EchoServer.cpp.patch'
)
makedepends=('autoconf-archive' 'wget' 'ruby' 'gperftools')
options=('!makeflags')
md5sums=('593ca1a6a28ec70b43fecffeb4667a8b'
         'f587fd8970bd02648474dac6d9d62326')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname"
  autoreconf -ivf
  cd httpserver/samples/echo
  patch --verbose -p0 < $srcdir/EchoServer.cpp.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname"
  PYTHON=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --with-gnu-ld=yes \
    --with-sysconfdir=/etc \
    --with-sysroot=/usr

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname"
  make DESTDIR="$pkgdir/" install
  # by default, 'make install' skips 'external' directory
  for i in http_parser_cpp.cpp http_parser.{c,h}; do
    install -Dm644 external/http_parser/$i $pkgdir/usr/include/external/http_parser/$i
  done
  install -Dm755 httpserver/samples/echo/echo_server $pkgdir/usr/bin/echo_server
}

# vim:set ts=2 sw=2 et:
